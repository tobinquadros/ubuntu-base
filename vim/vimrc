" $HOME/.vimrc
" Author: Tobin Quadros

" ==============================================================================
" RUNTIME SETUP
" ==============================================================================

set nocompatible " Turn off vi compatible mode

" Local directories, if turned on, will be located at
set runtimepath+=~/.vim/after " Add to distibuted $VIMRUNTIME files
set backupdir=~/.vim/backups
set directory=~/.vim/swaps
set undodir=~/.vim/undo

" Load bundles and plugin help docs, use pathogen#infect()
filetype off " Disbale before loading pathogen
execute pathogen#infect()
execute pathogen#helptags()
if has("autocmd")
  filetype plugin indent on
endif

" Load matchit.vim, but only if the user hasn't installed a newer version.
if !exists('g:loaded_matchit') && findfile('plugin/matchit.vim', &rtp) ==# ''
  runtime! macros/matchit.vim
endif

" Enable mouse
if has('mouse')
  set mouse=a
endif

" Use vertical windows for netrw preview window
let g:netrw_alto = 1
let g:netrw_altv = 1

" ==============================================================================
" COLOR
" ==============================================================================

set background=dark

" Set colorscheme for 256 color terminals
if &t_Co >= 256 || has("gui_running")
  colorscheme jellybeans
endif

" Enable highlighting for terminals with color
if &t_Co > 2 || has("gui_running")
  syntax enable
endif

" ==============================================================================
" OPTIONS
" ==============================================================================

" Editor windows
set wrap " Text wraps at terminal edge
set showbreak=... " Prefix for wrapped lines
set showmatch " Show bracket matching on insert
set matchtime=1
set list listchars=tab:→·,trail:·,nbsp:_,extends:» " show extra space characters
set number " Show line numbers
set scrolloff=2 " Scroll cursor padding (top and bottom)
set backspace=indent,eol,start " Allow backspacing over everything in insert mode
set autoindent " Copy indent when starting newline
set shiftround " Round to multiple of shiftwidth
set smartindent " Smarter autoindent
set smarttab " Smarter tabs and expanding
set tabstop=2 softtabstop=2 shiftwidth=2 expandtab " Global settings
set omnifunc=syntaxcomplete#Complete " Set omni-completion method.

" Statusbar
set laststatus=2 " Show statusbar
set ruler " Show statusbar (line,column) numbers
set statusline=%n\ %<%f\ %y\ %h%1*%m%*%r%=%-14.(%l,%c%V%)\ %P " Statusline attributes
set report=0 " Always show number of lines changed
set showcmd " Show commands (or selections) in last line of screen
set showmode " Show INSERT, VISUAL, etc., on last line of screen

" Ex and command line settings
set wildchar=<Tab> " Character pressed to use wildcard expansion
set wildmenu " Make menu available after wildchar press
set wildmode=longest:full,full " Wildchar functionality
set wildignore+=*.jpg,*.jpeg,*.gif,*.png,*.gif,*.psd,*.o,*.obj

" Registers and history
set history=1000 " Save last 1000 commands from command line
" set clipboard=unnamed " I believe this needs +xterm_clipboard compilation
set pastetoggle=<F3> " Configure pastemode for external copy and paste

" Buffers
set autoread " Outside changes will be loaded
set hidden " Allow window changes with unsaved buffers
set nobackup " No backup made
set nowritebackup " No backup made
set noswapfile " Don't write .swp files
set splitbelow " Split horizontal window below current
set splitright " Split vertical window to right of current

" Searching
set hlsearch " Highlight search matches
set ignorecase " Case-insensitive searches
set incsearch " Search results are shown as they are typed
set smartcase " Recognize case-sensitive input

" ==============================================================================
" FILETYPES & AUTOCOMMANDS
" To check native compilers run :args $VIMRUNTIME/compiler/*.vim
" ==============================================================================

" Syntastic
let g:syntastic_check_on_open=0 " Check syntax when file is opened
let g:syntastic_aggregate_errors = 1 " Allow multiple checkers
command! -nargs=* E Explore " Fix Explore conflict if needed

if has("autocmd")

  " Enter VIMRC au group
  augroup VIMRC

    " Clear VIMRC autocmd group command list
    autocmd!

    " Set FileTypes
    autocmd BufRead,BufNewFile .jshintrc set filetype=javascript
    " autocmd BufRead,BufNewFile *.jade set filetype=jade syntax=jade

    " Set FileType Syntax highlighting
    " autocmd Syntax json sou ~/.vim/syntax/json.vim

    " Set Filetype specific tab and spacing
    autocmd FileType text setlocal textwidth=80

    " Change statusline color based on insert mode
    if version >= 700
      autocmd InsertEnter * hi StatusLine term=reverse ctermfg=245 ctermbg=52
      autocmd InsertLeave * hi StatusLine term=reverse ctermfg=245 ctermbg=234
    endif

    " Restore cursor position from last session
    autocmd BufReadPost *
      \ if line("'\"") > 1 && line("'\"") <= line("$") |
      \   exe "normal! g`\"" |
      \ endif

    " Source .vimrc immediately after write
    autocmd BufWritePost .vimrc source %

  augroup end

endif

" ==============================================================================
" MAPPINGS & FUNCTIONS
" ==============================================================================

" Remap the Leader key to spacebar
let mapleader=" "

" Escape from insert mode
inoremap jk <ESC>

" Explore current directory
nnoremap <Leader>- :Explore<CR>
" Edit working directory
nnoremap <Leader>ew :edit .<CR>
" Edit .vimrc file
nnoremap <Leader>ev :edit $MYVIMRC<CR>
" Edit $HOME/dotfiles directory
nnoremap <Leader>ed :edit $HOME/dotfiles/<CR>

" Remove search highlights, keep history
nnoremap <Leader>hs :nohlsearch<CR>

" Insert divider for commenting
nnoremap <Leader>= 78i=<ESC>

" Run make
nnoremap <Leader>m :make<CR>

" Ctags search order, active buffer directory then current working directory
set tags=./tags,tags;
nnoremap <Leader>ct :!ctags -R .<CR>
" Ctags system tags file location (not stored in git repo)
set tags+=~/.vim/systags
cnoremap Sctags :!ctags -R -f ~/.vim/systags /usr/include /usr/local/include
" Tagbar plugin, command for quick open and close upon selection.
nnoremap <Leader>tb :TagbarOpenAutoClose<CR>

" Yank from cursor to end of line
nmap Y y$

" Allow ctrl-n/ctrl-p to filter in command line mode
cnoremap <C-p> <Up>
cnoremap <C-n> <Down>

" Open quickfix window, if window is open with no errors close window
nnoremap <Leader>Q :cwindow<CR>
" Close quickfix window
nnoremap <Leader>q :cclose<CR>

" Default orientation for vimux split pane
let g:VimuxOrientation = "h"
let g:VimuxHeight = "50"
" Run command in terminal runner pane
nnoremap <Leader>vc :call VimuxRunCommand("clear")<CR>
" Prompt for a command to run map
nnoremap <Leader>vp :VimuxPromptCommand<CR>
" Run last command executed by VimuxRunCommand
nnoremap <Leader>vl :VimuxRunLastCommand<CR>
" Inspect runner pane map
nnoremap <Leader>vi :VimuxInspectRunner<CR>
" Close vim tmux runner opened by VimuxRunCommand
nnoremap <Leader>vq :VimuxCloseRunner<CR>

" Search and replace word under cursor
nnoremap <Leader>* :%s/\<<C-r><C-w>\>//gc<LEFT><LEFT><LEFT>

" Sudo write
cnoremap SudoWrite w !sudo tee % >/dev/null

" Diff between modified buffer and saved file (\do)
if !exists(":DiffOrig")
  command DiffOrig vert new | set bt=nofile | r # | 0d_ | diffthis
    \ | wincmd p | diffthis
endif
nnoremap <Leader>do :DiffOrig<CR>

" Search todo's and place in quickfix list (\td)
function! GrepTodo()
  if exists("%")
    :vimgrep /todo/j  % **
  else
    :vimgrep /todo/j  **
  endif
  " Open the quickfix list
  :cwindow
endfunction
nnoremap <Leader>td :call GrepTodo()<CR>

" Strip trailing whitespace from lines (\ws)
function! StripWhitespace()
    let save_cursor = getpos(".")
    let old_query = getreg('/')
    :%s/\s\+$//e
    call setpos('.', save_cursor)
    call setreg('/', old_query)
endfunction
nnoremap <Leader>ws :call StripWhitespace()<CR>

" Toggle colorcolumn with filetype textwidth (\cc)
function! ToggleColorColumn()
  " If colorcolumn is off, turn it on
  if empty(&colorcolumn)
    " If colorcolumn is off and textwidth is not set then use colorcolumn=80
    if empty(&textwidth)
      echo "colorcolumn=80"
      set colorcolumn=80
    " If colorcolumn is off and textwidth is set the use colorcolumn=+1
    else
      echo "colorcolumn=+1 (" . (&textwidth + 1) . ")"
      set colorcolumn=+1
    endif
  " If colorcolumn is on then turn it off
  else
    echo "colorcolumn="
    set colorcolumn=
  endif
endfunction
nnoremap <Leader>cc :call ToggleColorColumn()<CR>
